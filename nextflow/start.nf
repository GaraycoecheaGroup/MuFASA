
include { MergeFastq; FastQC; CUTadapt} from './modules/FASTQprocessing.nf'
include { BWAMapping; Samtools_index; Samtools_sort; Samtools_fixmate; Picard_cleansam; Samtools_markdup } from './modules/Mapping.nf'
include { WgsMetrics; AlignmentSummary; PlotVAF } from './modules/Statistics.nf'
include { preVariantCalling; add_indel_snvs_tags; defineOrderISEC } from './modules/processing.nf'
include { Strelka; handleStrelka; Mutect2; Mutect2_flag; Mutect2_concat } from './modules/VariantCalling.nf'
include { PASSfilter; ISEC_overlap; PoN_filter; FiNGS; snvsVAF; handleFings; indelFiltering } from './modules/VariantFiltering.nf'

workflow {
	main:
	 
	names_paths = Channel.fromPath(params.samples)
				   		 .splitCsv()//.view()

	pair1 = names_paths.map{ it -> [it[0],params.fastq_path + it[1],"1"]}.groupTuple(by: 0)
	pair2 = names_paths.map{ it -> [it[0],params.fastq_path + it[2],"2"]}.groupTuple(by: 0)

	merge_input = pair1.concat(pair2).map{ it -> [it[0],it[1],it[2][0]]}

	// ------------FASTQ processing
	MergeFastq(merge_input) 
	merge_paired = MergeFastq.out.groupTuple(by:0)
	CUTadapt(merge_paired)
	fastqc_input = CUTadapt.out.flatMap{ sample_id, file_list -> file_list}
	FastQC(fastqc_input)

	// --------------MAPPING-------------------
	BWAMapping(CUTadapt.out) 
	Picard_cleansam(BWAMapping.out)
	Samtools_fixmate(Picard_cleansam.out)
	Samtools_sort(Samtools_fixmate.out)
	Samtools_markdup(Samtools_sort.out)
	bam_indexed = Samtools_index(Samtools_markdup.out)

	// -------------MAPPING METRICS -----------

	WgsMetrics(Samtools_markdup.out)
	AlignmentSummary(Samtools_markdup.out)

	// -------------VARIANT CALLING -----------
	// prepare channels for variant calling:
	chrs = Channel.from(params.chrs)//.view()
	sample_pairs = Channel
			.fromPath(params.strelka_conf)
			.splitCsv()//.view()

	// if normal is NOT in the mapped set:
	//extNormal = Channel
	//		.fromPath(params.ext_bam_conf)
	//		.splitCsv()

	//bamindexed = bam_indexed.concat(extNormal)//.view()  
	paired = bam_indexed.combine(sample_pairs,by:0).groupTuple(by: 4, size: 2)//.view()
	//ensure correct order: tumor normal
	ordered_paired = preVariantCalling(paired)
	
	// STRELKA variant calling//
	strelka_input = ordered_paired.map{ it -> [it[0][0],it[1][0],it[1][1],it[2][0],it[2][1]]}
	Strelka(strelka_input)
	strelka_out = handleStrelka(Strelka.out)
	
	// MUTECT //
	mutect_input = ordered_paired.map{ it -> [it[0][0],it[0][1],it[1][0],it[1][1]]}
	mut_paired = mutect_input.combine(chrs)
	Mutect2(mut_paired) //--> TO DO: seperate this, so Chr1, Chr2 are done with different time limit
	Mutect2.out.view()

	Mutect2_flag(Mutect2.out) // --> out: tumorID, *flagged.vcf.gz, chrID
	concat_input = Mutect2_flag.out.groupTuple(by: 0, size: params.nrChrs) // only start concatenate when all Chromosomes are done.
	Mutect2_concat(concat_input)//.view() // --> out: tumorID, indel.vcf.gz, snvs.vcf.gz
	
	// -------------VARIANT FILTERING -----------

	// WITHOUT MUTECT //
	//transposed = strelka_out.map{ it -> [it[0],it.drop(1)]}.transpose()
	
	// WITH MUTECT //
	// All output from Mutect and Strelka are combined to be able to go through filtering.	
	all_raw_variants = strelka_out.concat(Mutect2_concat.out)//.view()
	transposed = all_raw_variants.map{ it -> [it[0],it.drop(1)]}.transpose()// sampleid, path(.vcf.gz) both strelka and Mutect

	pass_input = add_indel_snvs_tags(transposed)
	PASSfilter(pass_input)
	isec_grouped = PASSfilter.out.groupTuple(by: [0,3], size: 2)//.view()
	  
	// rearrange for isec: strelka_mutect combinations and correct order: [sampleid, strelka_*, strelka_*.bai, mutect_*, mutect_*.bai, indel/snvs]
	isec_input = defineOrderISEC(isec_grouped)
	ISEC_overlap(isec_input)
	PoN_filter(ISEC_overlap.out)

	// ------------ SNV filtering ---------

	pon_snvs = PoN_filter.out.filter{ it.contains("snvs")}
	fings_input = pon_snvs.combine(strelka_input,by:0).map{ it -> [it[0],it[1],it[3],it[5],it[4],it[6]]}// -> id, vcffile, tumorbam, tumorbai, normalbam, normalbai
	FiNGS(fings_input)//.view()
	handleFings(FiNGS.out) // --> sample id, _fings.vcf
	snvsVAF(handleFings.out)  // --> out: sample_id, ${sample_id}.vaf.vcf
	
	toplot = pon_snvs.map{ listItem -> [listItem[0],listItem[1] , "PoN", "red"]}
	  .concat(handleFings.out.map{ listItem -> [listItem,params.snvs_filtered + "/" + listItem + "_fings.vcf", "FiNGS", "green"]})
	  .concat(snvsVAF.out.map{ listItem -> listItem + ["VAF", "blue"]})
	
	PlotVAF(toplot)

	// ------------ INDEL filtering ------

	pon_indels = PoN_filter.out.filter{ it.contains("indel")}
	indelFiltering(pon_indels)
}